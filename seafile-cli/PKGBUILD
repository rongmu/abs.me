# keep a close eye on seafile-shared:
# https://aur.archlinux.org/packages/seafile-shared
# Maintainer: Eric Yu <eric.rongmu@gmail.com>

pkgname=seafile-cli
pkgver=2.1.1
pkgrel=1
pkgdesc="seafile cli client"
arch=('i686' 'x86_64' 'armv7h')
url="https://github.com/haiwen/seafile/"
license=('GPL')
source=("https://github.com/haiwen/seafile/archive/v${pkgver}.tar.gz"
        "0002-Add-autoconfiguration-for-libjansson.patch")
depends=('ccnet>=1.4.0' 'libevent' 'python2-simplejson')
makedepends=(sed)
provides=(seafile-shared)
conflicts=(seafile-shared)
options=('!libtool' '!emptydirs')
md5sums=('f205f7e4088341a1805a725305b4cd37'
         '86357d9fee615ec3931cd2399fdcd4de')

prepare() {
  cd "$srcdir/seafile-$pkgver"

  sed -ri '1s/python.?$/python2/' ./app/seaf-cli
  patch -p1 -i "$srcdir/0002-Add-autoconfiguration-for-libjansson.patch"
}

build() {
  cd "$srcdir/seafile-$pkgver"

  ./autogen.sh
  ./configure --disable-gui --prefix=/usr PYTHON=/usr/bin/python2
  make -j1
}

package() {
  cd "$srcdir/seafile-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -D -m644 "$srcdir/seafile-$pkgver/LICENCE.txt" \
                   "$pkgdir/usr/share/licenses/$pkgname/LICENCE.txt"

  # remove unneeded desktop related files
  cd "$pkgdir/usr/share"

  rm -r applications icons pixmaps \
        man/man1/seafile-applet.1
}
