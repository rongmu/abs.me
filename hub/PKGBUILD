# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>

pkgname=hub
pkgver=1.11.1
pkgrel=1
pkgdesc="cli interface for Github"
arch=('any')
url="http://hub.github.com/"
depends=('git' 'ruby')
license=('MIT')
source=("$pkgname-$pkgver.tar.gz::https://github.com/github/hub/archive/v${pkgver}.tar.gz")
sha256sums=('96346e3a44849cb1b31bb2446ab9c32485a1665919ae61c7359dbdfc7eef9827')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  rake standalone
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"

  install -d "$pkgdir/usr/share/$pkgname"

  cp -dpr --no-preserve=ownership git-hooks "$pkgdir/usr/share/$pkgname/git-hooks"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -Dm644 etc/hub.bash_completion.sh "$pkgdir/usr/share/bash-completion/completions/hub"
  install -Dm644 etc/hub.zsh_completion "$pkgdir/usr/share/zsh/site-functions/_hub"

  install -Dm644 "man/$pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"
}
